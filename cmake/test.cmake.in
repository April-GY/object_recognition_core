if (ROS_GROOVY_FOUND)
  set(SETUP_DOT_SH ${CATKIN_BUILD_PREFIX}/setup.sh)
else()
  set(SETUP_DOT_SH ${CMAKE_BINARY_DIR}/setup.sh)
endif()

if (object_recognition_core_SOURCE_DIR)
if (ROS_GROOVY_FOUND)
  set(object_recognition_core_TEST_DIR ${CATKIN_BUILD_PREFIX}/share/object_recognition_core/test)
else()
  set(object_recognition_core_TEST_DIR ${CMAKE_BINARY_DIR}/share/test)
endif()
else()
  get_filename_component(SELF_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)
  set(object_recognition_core_TEST_DIR ${SELF_DIR}/../test)
endif()

#testing macros
macro(object_recognition_core_pytest pyfile)
  string(REPLACE ";" " " args "${ARGN}")
  add_test(${PROJECT_NAME}_${pyfile}.py
    ${object_recognition_core_TEST_DIR}/run_test.sh ${SETUP_DOT_SH} ${CMAKE_CURRENT_SOURCE_DIR}/${pyfile}.py ${args}
    )
endmacro()

macro(object_recognition_core_desktop_test pyfile)
  if(DESKTOP_TEST)
    string(REPLACE ";" " " args "${ARGN}")
    add_test(desktop_${PROJECT_NAME}_${pyfile}.py
      ${object_recognition_core_TEST_DIR}/run_test.sh ${SETUP_DOT_SH} ${CMAKE_CURRENT_SOURCE_DIR}/${pyfile}.py ${args}
      )
  endif(DESKTOP_TEST)
endmacro()

macro(object_recognition_core_data_download PATH_VAR DATA_FILE)
  set(data_base_url http://vault.willowgarage.com/wgdata1/vol1/ecto_data)
  set(${PATH_VAR} ${PROJECT_BINARY_DIR}/data/${DATA_FILE})
  if(NOT EXISTS ${${PATH_VAR}})
    message(STATUS "Data fetch.\n** Downloading:\n** ${data_base_url}/${DATA_FILE}\n** to:\n** ${${PATH_VAR}}")
    file(DOWNLOAD ${data_base_url}/${DATA_FILE} ${${PATH_VAR}})
  endif()
endmacro()

# macro testing a detection pipeline
macro(object_recognition_core_detection_test config_file)
  add_test(${PROJECT_NAME}_detection_${config_file}
    ${object_recognition_core_TEST_DIR}/run_test.sh ${SETUP_DOT_SH} "${object_recognition_core_TEST_DIR}/test_detection_pipeline.py -c ${config_file} --node_name \"\" --object_ids []"
    )
endmacro()

# macro testing a training pipeline
macro(object_recognition_core_training_test config_file)
  add_test(${PROJECT_NAME}_training_${config_file}
    ${object_recognition_core_TEST_DIR}/run_test.sh ${SETUP_DOT_SH} "${object_recognition_core_TEST_DIR}/test_training_pipeline.py -c ${config_file}"
    )
endmacro()

# macro testing a script with --help: "script" must contain the whole path
macro(object_recognition_core_help_test script)
  add_test(${PROJECT_NAME}_help_${script}
    ${object_recognition_core_TEST_DIR}/run_test.sh ${SETUP_DOT_SH} "${object_recognition_core_TEST_DIR}/test_help.py ${script}"
    )
endmacro()

# macro testing a Source
macro(object_recognition_core_source_test source_name)
  if (${ARGC} EQUAL 1)
    add_test(${PROJECT_NAME}_source_${source_name}
      ${object_recognition_core_TEST_DIR}/run_test.sh ${SETUP_DOT_SH} "${object_recognition_core_TEST_DIR}/test_source.py ${source_name}"
    )
  endif()
  if (${ARGC} EQUAL 2)
    add_test(${PROJECT_NAME}_source_${source_name}
      ${object_recognition_core_TEST_DIR}/run_test.sh ${SETUP_DOT_SH} "${object_recognition_core_TEST_DIR}/test_source.py ${source_name} ${ARGV1}"
    )
  endif()
  if (${ARGC} EQUAL 3)
    add_test(${PROJECT_NAME}_source_${source_name}
      ${object_recognition_core_TEST_DIR}/run_test.sh ${SETUP_DOT_SH} "${object_recognition_core_TEST_DIR}/test_source.py ${source_name} ${ARGV1} ${ARGV2}"
    )
  endif()
endmacro()

# macro testing a Sink
macro(object_recognition_core_sink_test sink_name)
  if (${ARGC} EQUAL 1)
    add_test(${PROJECT_NAME}_sink_${sink_name}
      ${object_recognition_core_TEST_DIR}/run_test.sh ${SETUP_DOT_SH} "${object_recognition_core_TEST_DIR}/test_sink.py ${sink_name}"
    )
  endif()
  if (${ARGC} EQUAL 2)
    add_test(${PROJECT_NAME}_sink_${sink_name}
      ${object_recognition_core_TEST_DIR}/run_test.sh ${SETUP_DOT_SH} "${object_recognition_core_TEST_DIR}/test_sink.py ${sink_name} ${ARGV1}"
    )
  endif()
  if (${ARGC} EQUAL 3)
    add_test(${PROJECT_NAME}_sink_${sink_name}
      ${object_recognition_core_TEST_DIR}/run_test.sh ${SETUP_DOT_SH} "${object_recognition_core_TEST_DIR}/test_sink.py ${sink_name} ${ARGV1} ${ARGV2}"
    )
  endif()
endmacro()
